rule download_blacklists:
    output:
        expand(
            "resources/blacklists/blacklist_{build}_v2.3.0.tsv.gz",
            build=["mm10_GRCm38", "hg19_hs37d5_GRCh37", "hg38_GRCh38", "mm39_GRCm39"],
        ),
    params:
        outdir=lambda wc, output: os.path.split(output[0])[0],
    log:
        "logs/download_arriba_blacklists.log",
    conda:
        "envs/curl.yaml"
    shell:
        """
        curl -L https://github.com/suhrig/arriba/releases/download/v2.3.0/arriba_v2.3.0.tar.gz | 
        tar -xzv --strip-components 2 -C {params.outdir} --wildcards "*blacklist*.tsv.gz" &> {log}
        """


rule star_index:
    input:
        fasta="resources/genome.fasta",
        annotation="resources/genome.gtf",
    output:
        directory("resources/star_genome"),
    threads: 4
    params:
        extra=lambda wc, input: f"--sjdbGTFfile {input.annotation} --sjdbOverhang 100",
    log:
        "logs/star_index_genome.log",
    cache: True  # mark as eligible for between workflow caching
    wrapper:
        "master/bio/star/index"


rule star_align:
    input:
        # use a list for multiple fastq files for one sample
        # usually technical replicates across lanes/flowcells
        fq1="reads/{sample}_R1.1.fastq",
        fq2="reads/{sample}_R2.1.fastq",  #optional
        idx="resources/star_genome",
        annotation="resources/genome.gtf",
    output:
        # see STAR manual for additional output files
        aln="star/{sample}/Aligned.out.bam",
        reads_per_gene="star/{sample}/ReadsPerGene.out.tab",
    log:
        "logs/star/{sample}.log",
    params:
        # specific parameters to work well with arriba
        extra=lambda wc, input: f"--quantMode GeneCounts --sjdbGTFfile {input.annotation}"
        " --outSAMtype BAM Unsorted --chimSegmentMin 10 --chimOutType WithinBAM SoftClip"
        " --chimJunctionOverhangMin 10 --chimScoreMin 1 --chimScoreDropMax 30 --chimScoreJunctionNonGTAG 0"
        " --chimScoreSeparation 1 --alignSJstitchMismatchNmax 5 -1 5 5 --chimSegmentReadGapMax 3",
    threads: 12
    wrapper:
        "master/bio/star/align"


rule arriba:
    input:
        bam="star/{sample}/Aligned.out.bam",
        genome="resources/genome.fasta",
        annotation="resources/genome.gtf",
        # optional blacklist file
        blacklist="resources/blacklists/blacklist_hg38_GRCh38_v2.3.0.tsv.gz",
    output:
        fusions="results/arriba/{sample}.fusions.tsv",
        discarded="results/arriba/{sample}.fusions.discarded.tsv",
    params:
        extra="",
    log:
        "logs/arriba/{sample}.log",
    threads: 1
    wrapper:
        "master/bio/arriba"
