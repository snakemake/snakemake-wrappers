rule mark_duplicates:
    input:
        bams="mapped/{sample}.bam",
    # optional to specify a list of BAMs; this has the same effect
    # of marking duplicates on separate read groups for a sample
    # and then merging
    output:
        bam="dedup/{sample}.bam",
        idx="dedup/{sample}.bai",
        metrics="dedup/{sample}.metrics.txt",
    log:
        "logs/dedup/{sample}.log",
    params:
        extra="--REMOVE_DUPLICATES true",
    # optional specification of memory usage of the JVM that snakemake will respect with global
    # resource restrictions (https://snakemake.readthedocs.io/en/latest/snakefiles/rules.html#resources)
    # and which can be used to request RAM during cluster job submission as `{resources.mem_mb}`:
    # https://snakemake.readthedocs.io/en/latest/executing/cluster.html#job-properties
    resources:
        mem_mb=1024,
    wrapper:
        "master/bio/picard/markduplicates"


use rule mark_duplicates as mark_duplicates_withmatecigar with:
    output:
        bam="dedup/{sample}.matecigar.bam",
        idx="dedup/{sample}.matecigar.bai",
        metrics="dedup/{sample}.matecigar.metrics.txt",
    log:
        "logs/dedup/{sample}.matecigar.log",
    params:
        withmatecigar=True,
        extra="--REMOVE_DUPLICATES true",


rule mark_duplicates_cram:
    input:
        bams="mapped/{sample}.bam",
        ref="ref/genome.fasta",
    # optional to specify a list of BAMs; this has the same effect
    # of marking duplicates on separate read groups for a sample
    # and then merging
    output:
        bam="dedup/{sample}.cram",
        idx="dedup/{sample}.crai",
        metrics="dedup/{sample}.cram.metrics.txt",
    log:
        "logs/dedup/{sample}.cram.log",
    params:
        extra="--REMOVE_DUPLICATES true",
        embed_ref=True,  # set true if the fasta reference should be embedded into the cram
        withmatecigar=False,
    # optional specification of memory usage of the JVM that snakemake will respect with global
    # resource restrictions (https://snakemake.readthedocs.io/en/latest/snakefiles/rules.html#resources)
    # and which can be used to request RAM during cluster job submission as `{resources.mem_mb}`:
    # https://snakemake.readthedocs.io/en/latest/executing/cluster.html#job-properties
    resources:
        mem_mb=1024,
    wrapper:
        "master/bio/picard/markduplicates"
